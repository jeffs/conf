# VARIABLES

CASE_SENSITIVE=true
COMPLETION_WAITING_DOTS=true
DISABLE_AUTO_TITLE=true
DISABLE_UNTRACKED_FILES_DIRTY=true

# Use precmd to format a nice separator for use in a colorized PROMPT.
typeset -H prompt_bar
PROMPT=$'\n%{$fg[cyan]%}(%h) $(hostname):$prompt_bar\n$%{$reset_color%} '
precmd() {
    local d=$(dirs -p | head -1)
    local -i n=$(($COLUMNS - ${#d} - ${#$(hostname)} - 10))
    prompt_bar="$d $(printf 'â”€%.0s' $(=/usr/bin/seq $n))"
}

# PLUGINS

# Note that zsh-syntax-highlighting must be the last plugin sourced.
# https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/INSTALL.md
plugins=(
    poetry
    zsh-syntax-highlighting
)

source $ZSH/oh-my-zsh.sh

eval "$(pyenv init -)"

# OPTIONS

setopt rmstarsilent	# Don't confirm rm *.

unsetopt autocd
unsetopt auto_pushd
unsetopt correct_all
unsetopt hist_verify
unsetopt share_history

zstyle ':completion:*' matcher-list ''

# Complete filenames after =, as in --someflag=.  Copied from:
# https://unix.stackexchange.com/a/599449/49952
autoload +X -U _default
functions[_default]="setopt local_options magic_equal_subst; $functions[_default]"

# https://python-poetry.org/docs/
fpath+=~/.zfunc
autoload -Uz compinit && compinit

# ALIASES

unalias l la

alias vim=nvim
alias e=${EDITOR:-nvim}
alias rust=evcxr  # Rust REPL
alias h=history
alias hd='hexdump -C'
alias u='c ..'
alias uu='c ../..'
alias uuu='c ../../..'
alias py='python -q'
alias r='R --no-save --quiet'
alias tf=terraform
for i in `seq 9`; do alias t$i="t -L$i"; done

alias g=git
for s in bl br ci clone co di flog glog push push-f show si since st stash; do alias $s="git $s"; done
alias pull='git pull --prune'
alias gp=git-prune

function re { cargo eval --expr "$*"; }

function b {
    if (( $# == 0 )); then
        bl
    else
        br "$@"
    fi
}

# TODO: If the argument to t is gitignored (as listed by git check-ignore),
# don't pass --git-ignore.

alias tree='exa --group-directories-first --tree --ignore-glob="__pycache__|dist|node_modules|target"'
alias t=tree

# FUNCTIONS

# Useful when copying lines of code from documentation that includes prompts,
# or from comments.
function $ { "$@" }
function // { "$@" }

c() { cd "$@" && ls; }
cg() { c $(git rev-parse --show-toplevel); }
cy() { cl --yesterday; }
l() { ls -hl --color "$@" | $PAGER }
la() { ls -Ahl --color "$@" | $PAGER }
mc() { mkdir -p $1 && c $1; }

# TODO: Support --reset and --rm flags.
# TODO: Ensure that this works with poetry.
#       https://python-poetry.org/docs/basic-usage/
play() {
    case $1; in
        c) e src/main.c;;
        cpp) e src/main.cpp ;;
        go) e src/main.go ;;
        html) e src/index.html ;;
        py)
            cd ~/var/play/py
            source $(poetry env info --path)/bin/activate
            e play/__main__.py
            ;;
        java) e src/main/java/Main.java ;;
        scala) e src/main/scala/Main.scala ;;
        js|node) node ;;
        rs|rust|'')
            cd ~/var/play/rs
            e src/main.rs
            ;;
    esac
}

# Load nvm lazily, since merely sourcing its definition takes ~350ms.
nvm() {
    # Install NVM if necessary.
    if [ ! -d ~/.nvm ]; then
        ~/conf/bin/install-node.zsh || return 1
    fi
    unset -f nvm
    export NVM_DIR=~/.nvm
    source $NVM_DIR/nvm.sh
    source $NVM_DIR/bash_completion
    nvm "$@"
    export PATH="$HOME/.nvm/versions/node/$(nvm current)/bin:$PATH"
}

# TODO: Specify --lts only if .nvmrc is not found in any ancestor directory.
for command in node npm npx yarn; do
    $command() {
        unset -f node npm npx yarn
        [[ -v NVM_DIR ]] || nvm use --lts
        "$0" "$@"
    }
done
unset command

# Load Scala support lazily, to avoid slowing shell startup.
sbt() {
    if ! whence -p sbt >&/dev/null; then
        export SDKMAN_DIR=~/.sdkman
        # Install sdkman, java, and sbt if necessary.
        if [ ! -d "$SDKMAN_DIR" ]; then
            ~/conf/bin/install-scala.zsh || return 1
        fi
        source "$SDKMAN_DIR"/bin/sdkman-init.sh
    fi
    unset -f sbt
    sbt "$@"
}

exa() {
    if ! /usr/bin/which exa; then
        cargo install exa
    fi
    unset -f exa
    exa "$@"
}

# Open a man page in Vim.
Man() { vim +":Man $1 | only"; }

# BINDINGS

if [ -d ~/opt/fzf ]; then
    source ~/opt/fzf/shell/completion.zsh 2>/dev/null
    source ~/opt/fzf/shell/key-bindings.zsh
fi

# https://github.com/ohmyzsh/ohmyzsh/issues/5071
bindkey '^[l' down-case-word

# Stolen by FZF.  Steal it back.
bindkey '^[c' capitalize-word
