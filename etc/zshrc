## HISTORY

HISTFILE=~/.zsh_history
HISTSIZE=50000
SAVEHIST=50000
setopt append_history
setopt hist_ignore_dups
setopt hist_ignore_space

## COMPLETION

autoload -Uz compinit && compinit

## PROMPT

PROMPT=$'\n%F{cyan}%~%f\n%# '

## OPTIONS

setopt rmstarsilent	# Don't confirm rm *.

unsetopt autocd
unsetopt auto_pushd
unsetopt correct_all
unsetopt hist_verify
unsetopt share_history

zstyle ':completion:*' matcher-list ''

# Complete filenames after =, as in --someflag=.  Copied from:
# https://unix.stackexchange.com/a/599449/49952
autoload +X -U _default
functions[_default]="setopt local_options magic_equal_subst; $functions[_default]"

## ALIASES

alias e=edit # ../prj/edit
alias vim=nvim
alias rust='evcxr -q' # Rust REPL
alias h=history
alias hd='hexdump -C'
alias ls='eza -F'
alias tf=terraform

### Git

alias g=git
alias di='git di'
alias show='git show'
alias pull='git pull --prune'

for s in br ci clone co flog glog glog4 push push-f st stash; do
    alias $s="git $s"
done

for i in $(seq 10); do
    alias glog$i="glog -$i"
done

if [[ "$TERM_PROGRAM" == vscode ]]; then
    # Prefix matches with `file:lineno:` so I can click to open.
    alias rg='rg --no-heading'
fi

alias yolo='ci -a --amend --no-edit --no-verify && push -f'

## FUNCTIONS

# Strip comment leaders from copy/pasted sample code.
function $ { "$@" }
function // { "$@" }

function c { cd "$@" && ls; }
function cg { c $(git rev-parse --show-toplevel); }
function mc { mkdir -p $1 && c $1; }

function l { ls -hl --color=always "$@" | $PAGER }
function la { ls -Ahl --color=always "$@" | $PAGER }

## PROFILES
#
# Extensions and overrides for specific projects.  Checking out different
# profiles on different laptops allows customization for home, work, etc.

for conf_module in ~/git/conf-*(N); do
    if [ -r $conf_module/etc/zshrc ]; then
        source $conf_module/etc/zshrc
    fi
done
